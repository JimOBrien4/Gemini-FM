	[number match level address z80 print disk full map]
BEGIN
	{---------------------------------}
	{-- PLACE OUR PROGRAM AT ^h8000 --}
	{---------------------------------}
	STRING RESERVE_SPACE LENGTH ^h7EF9;
	
	{-----------------------------------}
	{-- CONSTANTS AND WORKING STORAGE --}
	{-----------------------------------}
	SET DOS_CALLF = ^h30;
	SET DOS_RDSLT = ^h09;
	SET DOS_WRSLT = ^h11;
	
	SET CALL_OPCODE = ^hCD;
	SET RET_OPCODE =  ^hC9;
	
	SET DOS2_BIOS = ^h0005;
	
	SET HIGH = ^hFF;
	SET LOW = ^h00;
	
	SET DETECT_APRLOPLL_ADDR = ^h4018;
	SET DETECT_MCHFM0_ADDR = ^h0080;
	
	BYTE WHILE_LOOP_COUNTER VALUE 0;
	BYTE WHILE_LOOP_LIMIT VALUE 0;
	BYTE DETECT_SLOT VALUE 0;
	
	STRING FOUND_STRING LENGTH 9;
	STRING GOAL_STRING LENGTH 9;
	POINTER TO BYTE INTERNAL_DETECT_POINTER VALUE #FOUND_STRING;
	WORD DETECT_ADDR;
	WORD EXTERNAL_DETECT_POINTER;
	BYTE DETECT_STRING_LENGTH;
	BYTE DETECT_CHAR_IN;
	
	EXTERNAL BYTE FM_PAC_ENABLE ADDRESS ^h7FF6;

	WORD DUMMY_WORD;
	STRING STR_OUT LENGTH 15;
	
	WORD GEMINI_COMMAND;
	REDEFINE GEMINI_COMMAND;
		RECORD GEMINI_COMMAND_REC;
			BYTE GEMINI_COMMAND_BYTECODE;
			BYTE GEMINI_COMMAND_DATA;
		ENDREC;
	ENDREDEF;
	POINTER TO WORD GEMINI_COMMAND_POINTER VALUE HIMEM;
	WORD GEMINI_INFINITE_LOOP_POINT;
	BYTE TEMPO VALUE 0;
	BYTE TEMPO_COUNTER VALUE 0;
	BYTE WAIT_COUNT;
	BYTE GEMINI_NOT_PAUSED VALUE 0;
	
	FIELD LOOP_STACK LENGTH 32;
	POINTER TO WORD LOOP_STACK_POINTER VALUE #LOOP_STACK;
	
	BYTE LASTCHAR;
	BYTE INCHAR;
	STRING INBUFFER 512;
	FILE INFILE DISK FILE1 TEXT RECORD INCHAR BUFFER INBUFFER VALUE "SONG.BIN";
	EXTERNAL WORD TPA_END ADDRESS ^h0006;
	POINTER TO BYTE SONG_LOAD_POINTER VALUE HIMEM;
	WORD USER_AREA_END;
	
	EXTERNAL FIELD HTIMI_HOOK ADDRESS ^hFD9F LENGTH 5;
	FIELD OLD_HTIMI_HOOK 5;
	BYTE OLD_HTIMI_RET VALUE RET_OPCODE;
	FIELD NEW_HTIMI_HOOK 5;
	REDEFINE NEW_HTIMI_HOOK;
		RECORD NEW_HTIMI_HOOK_REC;
			BYTE NEW_HTIMI_COMMAND_CALL VALUE CALL_OPCODE;
			WORD NEW_HTIMI_COMMAND_ADDRESS VALUE VBLANK;
			BYTE NEW_HTIMI_COMMAND_RET VALUE RET_OPCODE;
			BYTE FILLER VALUE ^h00;
		ENDREC;
	ENDREDEF;
	
	{------------------
	{-- OPLL DEFINES --
	{------------------
	SET OPLL_FNUM_C = 87;
	SET OPLL_FNUM_CSHARP = 91;
	SET OPLL_FNUM_D = 97; 
	SET OPLL_FNUM_DSHARP = 103; 
	SET OPLL_FNUM_E = 109;
	SET OPLL_FNUM_F = 115; 
	SET OPLL_FNUM_FSHARP = 122; 
	SET OPLL_FNUM_G = 129; 
	SET OPLL_FNUM_GSHARP = 137; 
	SET OPLL_FNUM_A = 145;
	SET OPLL_FNUM_ASHARP = 154; 
	SET OPLL_FNUM_B = 163; 
	
	SET OPLL_REG_PORT = ^h7C;
	SET OPLL_DATA_PORT = ^h7D;
	
	SET OPLL_RHYTHM_BASE = ^h0E;
	SET OPLL_FNUM_BASE = ^h10;
	SET OPLL_CNTL_BASE = ^h20;
	SET OPLL_INSTVOL_BASE = ^h30;
	
	BYTE OPLL_REG_OUT;
	BYTE OPLL_DATA_OUT;
	BYTE OPLL_CHANNEL;
	BYTE OPLL_NOTE;
	BYTE OPLL_OCTAVE;
	
	POINTER TO BYTE OPLL_FNUM;
	POINTER TO BYTE OPLL_CNTL;
	POINTER TO BYTE OPLL_INSTVOL;
	POINTER TO BYTE OPLL_DETUNE;
	
	BYTE OPLL0_FNUM;
	BYTE OPLL0_CNTL;
	BYTE OPLL0_INSTVOL;
	BYTE OPLL0_DETUNE;
	
	BYTE OPLL1_FNUM;
	BYTE OPLL1_CNTL;
	BYTE OPLL1_INSTVOL;
	BYTE OPLL1_DETUNE;
	
	BYTE OPLL2_FNUM;
	BYTE OPLL2_CNTL;
	BYTE OPLL2_INSTVOL;
	BYTE OPLL2_DETUNE;
	
	BYTE OPLL3_FNUM;
	BYTE OPLL3_CNTL;
	BYTE OPLL3_INSTVOL;
	BYTE OPLL3_DETUNE;
	
	BYTE OPLL4_FNUM;
	BYTE OPLL4_CNTL;
	BYTE OPLL4_INSTVOL;
	BYTE OPLL4_DETUNE;
	
	BYTE OPLL5_FNUM;
	BYTE OPLL5_CNTL;
	BYTE OPLL5_INSTVOL;
	BYTE OPLL5_DETUNE;
	
	BYTE OPLL6_FNUM;
	BYTE OPLL6_CNTL;
	BYTE OPLL6_INSTVOL;
	BYTE OPLL6_DETUNE;
	
	BYTE OPLL_RHYTHM;
	BYTE OPLL_BD_VOL;
	BYTE OPLL_SD_VOL;
	BYTE OPLL_HH_VOL;
	BYTE OPLL_TM_VOL;
	BYTE OPLL_CY_VOL;
	
	{----------------------------------------------------------}
	{-- FIRST WE SEARCH FOR A YAMAHA SFG DEVICE IN ALL SLOTS --}
	{----------------------------------------------------------}
	MOVE "MCHFM0" TO GOAL_STRING;
	
	MOVE 6 TO DETECT_STRING_LENGTH;
	MOVE 4 TO WHILE_LOOP_LIMIT;
	MOVE DETECT_MCHFM0_ADDR TO DETECT_ADDR;
	
	CALL SUB_SEARCH_FOR_DETECTION_STRING;
	
	MOVE 128 TO DETECT_SLOT;
	MOVE 144 TO WHILE_LOOP_LIMIT;
	
	CALL SUB_SEARCH_FOR_DETECTION_STRING;
	
	{---------------------------------------------------------------}
	{-- NOW WE SEARCH FOR INTERNAL OR CLONE MSX-MUSIC DEVICE      --}
	{-- THESE CAN BE USED IMMEDIATELY AND SHOULD NOT BE ACTIVATED --}
	{---------------------------------------------------------------}
	MOVE "APRLOPLL" TO GOAL_STRING;
	MOVE 8 TO DETECT_STRING_LENGTH;
	MOVE 4 TO WHILE_LOOP_LIMIT;
	MOVE DETECT_APRLOPLL_ADDR TO DETECT_ADDR;
	
	CALL SUB_SEARCH_FOR_DETECTION_STRING;
	
	MOVE 128 TO DETECT_SLOT;
	MOVE 144 TO WHILE_LOOP_LIMIT;
	
	CALL SUB_SEARCH_FOR_DETECTION_STRING;
	
	{---------------------------------------------------------------}
	{-- NOW WE SEARCH FOR PANASOFT FM-PAC, WHICH MUST BE ENABLED  --}
	{---------------------------------------------------------------}
	MOVE "PAC2OPLL" TO GOAL_STRING;
	MOVE 4 TO WHILE_LOOP_LIMIT;
	CALL SUB_SEARCH_FOR_DETECTION_STRING;
	
	MOVE 128 TO DETECT_SLOT;
	MOVE 144 TO WHILE_LOOP_LIMIT;
	CALL SUB_SEARCH_FOR_DETECTION_STRING;
	
	GOTO END;
	
	TERMINATE:
	{------------------------------------------------------------}
	{-- TERMINATION HANDLER                                    --}
	{------------------------------------------------------------}
		DISABLE INTERRUPTS;
		MOVE OLD_HTIMI_HOOK TO HTIMI_HOOK;
		MOVE ^h30 TO GEMINI_COMMAND_BYTECODE;
		MOVE ^h00 TO GEMINI_COMMAND_DATA;
		
		WHILE GEMINI_COMMAND_BYTECODE < ^h3B DO
			CALL SUB_OPLL_SET_VOLUME;
			ADD 1 TO GEMINI_COMMAND_BYTECODE;
		OD;
		
		MOVE 0 TO GEMINI_COMMAND_DATA;
			WHILE GEMINI_COMMAND_DATA < 7 DO
			CALL SUB_OPLL_NOTE_OFF;
			ADD 1 TO GEMINI_COMMAND_DATA;
		OD;
		ENABLE INTERRUPTS;
		DISPLAY "PERFORMANCE COMPLETE"
	GOTO END;
	
	PAC2_ACTIVATE:
	{------------------------------------------------------------}
	{-- INTERSLOT CALL TO OPLINI FM-BIOS ROUTINE               --}
	{------------------------------------------------------------}
		MCALL DOS_CALLF;
		BYTE OPLLSLOT;
		WORD ADR VALUE ^h5009;
	EXIT;
	
	LABEL_DEVICE_FOUND:
	{------------------------------------------------------}
	{-- WE FOUND AN MSX-MUSIC DEVICE. ENABLE IF PANASOFT --}
	{------------------------------------------------------}
	ENABLE INTERRUPTS;
	CONVERT DETECT_SLOT TO HEX STR_OUT;
	DISPLAY FOUND_STRING," found in slot ",STR_OUT;
	IF FOUND_STRING = "PAC2OPLL" THEN
		DISPLAY "ENABLING FM-PAC...";
		MOVE DETECT_SLOT TO OPLLSLOT;
		CALL PAC2_ACTIVATE;
	FI;
	
	{-------------------------------}
	{-- INITIALIZE FM DRUM VALUES --}
	{-------------------------------}
	DISPLAY "INITIALIZING OPLL...";
	MOVE ^h0E TO OPLL_REG_OUT;
	MOVE ^h20 TO OPLL_DATA_OUT;
	CALL SUB_OPLL_WRITE;
	MOVE ^h16 TO OPLL_REG_OUT;
	MOVE ^h20 TO OPLL_DATA_OUT;
	CALL SUB_OPLL_WRITE;
	MOVE ^h17 TO OPLL_REG_OUT;
	MOVE ^h50 TO OPLL_DATA_OUT;
	CALL SUB_OPLL_WRITE;
	MOVE ^h18 TO OPLL_REG_OUT;
	MOVE ^hC0 TO OPLL_DATA_OUT;
	CALL SUB_OPLL_WRITE;
	MOVE ^h26 TO OPLL_REG_OUT;
	MOVE ^h05 TO OPLL_DATA_OUT;
	CALL SUB_OPLL_WRITE;
	MOVE ^h27 TO OPLL_REG_OUT;
	MOVE ^h05 TO OPLL_DATA_OUT;
	CALL SUB_OPLL_WRITE;
	MOVE ^h28 TO OPLL_REG_OUT;
	MOVE ^h01 TO OPLL_DATA_OUT;
	CALL SUB_OPLL_WRITE;
	
	{----------}
	{-- MAIN --}
	{----------}
	
	{-- ASSIGN VBLANK INTERUPT HANDLER AND PROGRAM TERMINATE INTERRUPT HANDLER
	DISABLE INTERRUPTS;
	MOVE HTIMI_HOOK TO OLD_HTIMI_HOOK;
	MOVE NEW_HTIMI_HOOK TO HTIMI_HOOK;
	ENABLE INTERRUPTS;
	MCALL DOS2_BIOS USING ^h63, #TERMINATE;
	
	{-- LOAD THE PERFORMANCE DATA INTO USER AREA OF RAM
	{-- THE STACK EXISTS IN THE USER AREA SO LET'S GIVE IT 500 BYTES TO BE SAFE
	SUBTRACT ^h500 FROM TPA_END GIVING USER_AREA_END;
	
	DISPLAY "LOADING PERFORMANCE DATA..."
	OPEN INFILE;
	DO
		IF SONG_LOAD_POINTER = USER_AREA_END THEN
			CLOSE INFILE;
			DISPLAY "!!! OUT OF MEMORY !!!";
			GOTO END;
		FI;
		MOVE INCHAR TO LASTCHAR;
		READ INFILE;
		
		IF INCHAR = ^h1A THEN
			IF LASTCHAR = ^h1A THEN
				EXITDO;
			FI;
		FI;
		
		MOVE INCHAR TO @SONG_LOAD_POINTER;
		ADD 1 TO SONG_LOAD_POINTER;

	OD;
	CLOSE INFILE;
	
	DISPLAY "SONG DATA START ADDRESS:";
	CONVERT GEMINI_COMMAND_POINTER TO HEX STR_OUT;
	DISPLAY STR_OUT;
	DISPLAY "SONG DATA END ADDRESS:"
	CONVERT SONG_LOAD_POINTER TO HEX STR_OUT;
	DISPLAY STR_OUT;
	MOVE 1 TO GEMINI_NOT_PAUSED;
	
	DISPLAY "PLAYING...";
	DISPLAY " ";
	MAIN:
	
	{-- IF THE PERFORMANCE ROUTINE PAUSES THE MUSIC, END THE PROGRAM
	IF GEMINI_NOT_PAUSED = 0 THEN
		GOTO TERMINATE;
	FI;
	
	IF GEMINI_COMMAND_POINTER > SONG_LOAD_POINTER THEN
		GOTO TERMINATE;
	FI;
	
	{-- MESS WITH THE CURSOR- THIS IS USER'S OPPORTUNITY TO HIT CNTL+C TO QUIT
	DISPLAY "~1E~";
	GOTO MAIN;
	GOTO END;
	
	VBLANK:
	{--------------------}
	{-- VBLANK HANDLER --}
	{--------------------}
		IF GEMINI_NOT_PAUSED <> 0 THEN
			IF TEMPO_COUNTER = TEMPO THEN
				MOVE 0 TO TEMPO_COUNTER;
				IF WAIT_COUNT > 0 THEN
					SUBTRACT 1 FROM WAIT_COUNT;
				ELSE
					WHILE WAIT_COUNT < 1 DO
						MOVE @GEMINI_COMMAND_POINTER TO GEMINI_COMMAND;
						SWITCH ON GEMINI_COMMAND_BYTECODE:
							^h01: MOVE GEMINI_COMMAND_DATA TO TEMPO;
							^h02: CALL SUB_OPLL_NOTE_OFF;
							^h03: MOVE GEMINI_COMMAND_DATA TO WAIT_COUNT;
							^h04: CALL SUB_SET_LOOP;
							^h0F: MOVE 0 TO GEMINI_NOT_PAUSED;
							^h10 - ^h16: CALL SUB_OPLL_NOTE_ON;
							^h20 - ^h25: CALL SUB_OPLL_SET_INSTRUMENT;
							^h30 - ^h3A: CALL SUB_OPLL_SET_VOLUME;
							^h40 - ^h46: CALL SUB_SET_DETUNE;
						ENDSWITCH;
						ADD 2 TO GEMINI_COMMAND_POINTER;
					OD;
				FI;
			ELSE
				ADD 1 TO TEMPO_COUNTER;
			FI;
		FI;
	EXIT;
	
	{--------------------------------
	{-- PROCEDURES AND SUBROUTINES --
	{--------------------------------
	
	SUB_SEARCH_FOR_DETECTION_STRING:
	{------------------------------------------------}
	{-- LOOP THROUGH ALL SLOTS TO FIND GOAL STRING --}
	{-- INPUT: DETECT_SLOT, LOOP_LIMT, GOAL STRING --}
	{-- OUTPUT: N/A                                --}
	{-- CALLS: SUB_DETECT_STRING_SLOT_SEARCH     --}
	{------------------------------------------------}
		WHILE DETECT_SLOT < WHILE_LOOP_LIMIT DO
			CALL SUB_DETECT_STRING_SLOT_SEARCH;
			ENABLE INTERRUPTS;
			IF FOUND_STRING = GOAL_STRING THEN
				POP DUMMY_WORD;
				GOTO LABEL_DEVICE_FOUND;
			FI;
			ADD 1 TO DETECT_SLOT;
		OD;
	EXIT;
	
	SUB_DETECT_STRING_SLOT_SEARCH:
	{-------------------------------------}
	{-- SEARCH SLOT FOR GOAL STRING     --}
	{-- INPUT: DETECT_ADDR, DETECT_SLOT --}
	{-- OUTPUT: DETECT_STRING           --}
	{-------------------------------------}
		MOVE 0 TO WHILE_LOOP_COUNTER;
		MOVE #FOUND_STRING TO INTERNAL_DETECT_POINTER;
		MOVE DETECT_ADDR TO EXTERNAL_DETECT_POINTER;
		WHILE WHILE_LOOP_COUNTER < DETECT_STRING_LENGTH;
			MCALL DOS_RDSLT USING ,,EXTERNAL_DETECT_POINTER,DETECT_SLOT GIVING ,,,DETECT_CHAR_IN;
			MOVE DETECT_CHAR_IN TO @INTERNAL_DETECT_POINTER;
			ADD 1 TO INTERNAL_DETECT_POINTER;
			ADD 1 TO EXTERNAL_DETECT_POINTER;
			ADD 1 TO WHILE_LOOP_COUNTER;
		OD;
	EXIT;
	
	SUB_OPLL_WRITE:
	{----------------------------------------}
	{-- WRITE A REG/DATA PAIR TO OPLL      --}
	{-- INPUT: OPLL_REG_OUT, OPLL_DATA_OUT --}
	{-- OUTPUT: none                       --}
	{-- CALLS: SUB_WAIT                  --}
	{----------------------------------------}
		{--CONVERT OPLL_REG_OUT TO HEX STR_OUT;
		{--ENABLE INTERRUPTS;
		{--DISPLAY "WRITING ", STR_OUT, " TO PORT 7C";
		{--DISABLE INTERRUPTS;
		
		OUTPUT OPLL_REG_OUT TO OPLL_REG_PORT;
		MOVE 1 TO WHILE_LOOP_COUNTER;
		CALL SUB_WAIT;
		
		{--CONVERT OPLL_DATA_OUT TO HEX STR_OUT;
		{--ENABLE INTERRUPTS;
		{--DISPLAY "WRITING ", STR_OUT, " TO PORT 7D";
		{--DISABLE INTERRUPTS;
		
		OUTPUT OPLL_DATA_OUT TO OPLL_DATA_PORT;
		MOVE 1 TO WHILE_LOOP_COUNTER;
		CALL SUB_WAIT;
	EXIT;
	
	SUB_WAIT:
	{-------------------------------------}
	{-- WAIT APPROX 100 CYCLES PER LOOP --}
	{-- INPUT: WHILE_LOOP_COUNTER             --}
	{-- OUTPUT: WHILE_LOOP_COUNTER = 0        --}
	{-------------------------------------}
		IF WHILE_LOOP_COUNTER > 0 THEN;
			SUBTRACT 1 FROM WHILE_LOOP_COUNTER;
			GOTO SUB_WAIT;
		FI;
	EXIT;
	
	SUB_OPLL_NOTE_OFF:
	{---------------------------------------------------------------}
	{-- ISSUE NOTE OFF COMMAND TO SPECIFIED CHANNEL               --}
	{-- INPUT: GEMINI_COMMAND_DATA                                --}
	{-- OUTPUT: OPLLX_CNTL                                        --}
	{-- USES: OPLL_CNTL, OPLL_DATA_OUT, OPLL_REG_OUT, OPLL_RHYTHM --}
	{-- CALLS: SUB_OPLL_WRITE                                     --}
	{---------------------------------------------------------------}
		IF GEMINI_COMMAND_DATA <> 6 THEN
			MOVE GEMINI_COMMAND_DATA TO OPLL_CNTL;
			MULTIPLY OPLL_CNTL BY 4;
			ADD #OPLL0_CNTL TO OPLL_CNTL;
			AND @OPLL_CNTL WITH ^b11101111
			MOVE @OPLL_CNTL TO OPLL_DATA_OUT;
			MOVE GEMINI_COMMAND_DATA TO OPLL_REG_OUT;
			ADD OPLL_CNTL_BASE TO OPLL_REG_OUT;
			CALL SUB_OPLL_WRITE;
		ELSE
			MOVE ^b00100000 TO OPLL_RHYTHM;
			MOVE OPLL_RHYTHM TO OPLL_DATA_OUT;
			MOVE OPLL_RHYTHM_BASE TO OPLL_REG_OUT;
			CALL SUB_OPLL_WRITE;
		FI;
	EXIT;
	
	SUB_SET_LOOP:
	{------------------------------------------------------------------------------------}
	{-- PROCESS LOOP COMMNAND                                                          --}
	{-- INPUT/OUTPUT: BASICALLY ANYTHING TO DO WITH EXECUTION ORDER AND THE LOOP STACK --}
	{------------------------------------------------------------------------------------}
		SWITCH ON GEMINI_COMMAND_DATA:
			0: MOVE GEMINI_COMMAND_POINTER TO GEMINI_INFINITE_LOOP_POINT;
			1: MOVE GEMINI_INFINITE_LOOP_POINT TO GEMINI_COMMAND_POINTER;
			2: BEGIN
				ADD 1 TO LOOP_STACK_POINTER;
				MOVE GEMINI_COMMAND_POINTER TO @LOOP_STACK_POINTER;
			END;
			3: BEGIN
				IF @LOOP_STACK_POINTER = ^hFFFF THEN
					SUBTRACT 1 FROM LOOP_STACK_POINTER;
				ELSE
					MOVE @LOOP_STACK_POINTER TO GEMINI_COMMAND_POINTER;
					MOVE ^hFFFF TO @LOOP_STACK_POINTER;
				FI;
			END;
		ENDSWITCH;	
	EXIT;
		
	SUB_OPLL_NOTE_ON:
	{--------------------------------------------------------------------------}
	{-- CALCULATE FNUM AND ISSUE NOTE ON FOR GIVEN NOTE IN GIVEN CHANNEL     --}
	{-- INPUT: GEMINI_COMMAND_BYTECODE, GEMINI_COMMAND_DATA                  --}
	{-- OUTPUT: OPLLX_FNUM, OPLLX_CNTL                                       --}
	{-- USES: OPLL_FNUM, OPLL_CNTL, OPLL_DATA_OUT, OPLL_REG_OUT, OPLL_RHYTHM --}
	{-- CALLS: SUB_OPLL_WRITE                                                --}
	{--------------------------------------------------------------------------}
		SUBTRACT ^h10 FROM GEMINI_COMMAND_BYTECODE;
		
		IF GEMINI_COMMAND_BYTECODE <> 6 THEN
			MOVE GEMINI_COMMAND_BYTECODE TO OPLL_CNTL;
			MULTIPLY OPLL_CNTL BY 4;
			MOVE OPLL_CNTL TO OPLL_FNUM;
			MOVE OPLL_CNTL TO OPLL_DETUNE;
			ADD #OPLL0_CNTL TO OPLL_CNTL;
			ADD #OPLL0_FNUM TO OPLL_FNUM;
			ADD #OPLL0_DETUNE TO OPLL_DETUNE;
			DIVIDE GEMINI_COMMAND_DATA BY 12 GIVING OPLL_OCTAVE REMAINDER OPLL_NOTE;
			SWITCH ON OPLL_NOTE:
				0: MOVE OPLL_FNUM_C TO @OPLL_FNUM;
				1: MOVE OPLL_FNUM_CSHARP TO @OPLL_FNUM;
				2: MOVE OPLL_FNUM_D TO @OPLL_FNUM;
				3: MOVE OPLL_FNUM_DSHARP TO @OPLL_FNUM;
				4: MOVE OPLL_FNUM_E TO @OPLL_FNUM;
				5: MOVE OPLL_FNUM_F TO @OPLL_FNUM;
				6: MOVE OPLL_FNUM_FSHARP TO @OPLL_FNUM;
				7: MOVE OPLL_FNUM_G TO @OPLL_FNUM;
				8: MOVE OPLL_FNUM_GSHARP TO @OPLL_FNUM;
				9: MOVE OPLL_FNUM_A TO @OPLL_FNUM;
				10: MOVE OPLL_FNUM_ASHARP TO @OPLL_FNUM;
				11: MOVE OPLL_FNUM_B TO @OPLL_FNUM;
			ENDSWITCH;
			MOVE @OPLL_FNUM TO OPLL_DATA_OUT;
			SUBTRACT @OPLL_DETUNE FROM OPLL_DATA_OUT;
			MOVE OPLL_FNUM_BASE TO OPLL_REG_OUT;
			ADD GEMINI_COMMAND_BYTECODE TO OPLL_REG_OUT;
			CALL SUB_OPLL_WRITE;
			
			ADD OPLL_OCTAVE TO OPLL_OCTAVE;
			AND @OPLL_CNTL WITH ^b11110001;
			OR @OPLL_CNTL WITH OPLL_OCTAVE;
			OR @OPLL_CNTL WITH ^b00010000;
			MOVE @OPLL_CNTL TO OPLL_DATA_OUT;
			MOVE OPLL_CNTL_BASE TO OPLL_REG_OUT;
			ADD GEMINI_COMMAND_BYTECODE TO OPLL_REG_OUT;
			 CALL SUB_OPLL_WRITE;
			
		ELSE
			MOVE GEMINI_COMMAND_DATA TO OPLL_RHYTHM;
			MOVE OPLL_RHYTHM TO OPLL_DATA_OUT;
			MOVE OPLL_RHYTHM_BASE TO OPLL_REG_OUT;
			CALL SUB_OPLL_WRITE;
		FI;
	EXIT;
	
	SUB_OPLL_SET_INSTRUMENT:
	{--------------------------------}
	{-- SET CHANNEL INSTRUMENT     --}
	{--------------------------------}
		SUBTRACT ^h20 FROM GEMINI_COMMAND_BYTECODE;
		MOVE GEMINI_COMMAND_BYTECODE TO OPLL_INSTVOL;
		MULTIPLY OPLL_INSTVOL BY 4;
		ADD #OPLL0_INSTVOL TO OPLL_INSTVOL;
		AND @OPLL_INSTVOL WITH ^h0F;
		MULTIPLY GEMINI_COMMAND_DATA BY ^h10;
		OR @OPLL_INSTVOL WITH GEMINI_COMMAND_DATA;
		
		MOVE GEMINI_COMMAND_BYTECODE TO OPLL_REG_OUT;
		ADD OPLL_INSTVOL_BASE TO OPLL_REG_OUT;
		MOVE @OPLL_INSTVOL TO OPLL_DATA_OUT;
		CALL SUB_OPLL_WRITE;
	EXIT;
	
	SUB_OPLL_SET_VOLUME:
	{--------------------------------}
	{-- SET CHANNEL VOLUME         --}
	{--------------------------------}
		SUBTRACT ^h30 FROM GEMINI_COMMAND_BYTECODE;
		SUBTRACT GEMINI_COMMAND_DATA FROM 15 GIVING GEMINI_COMMAND_DATA
		SWITCH ON GEMINI_COMMAND_BYTECODE:
			0,1,2,3,4,5: BEGIN
				MOVE GEMINI_COMMAND_BYTECODE TO OPLL_INSTVOL;
				MULTIPLY OPLL_INSTVOL BY 4;
				ADD #OPLL0_INSTVOL TO OPLL_INSTVOL;
				AND @OPLL_INSTVOL WITH ^hF0;
				OR @OPLL_INSTVOL WITH GEMINI_COMMAND_DATA;
				
				MOVE GEMINI_COMMAND_BYTECODE TO OPLL_REG_OUT;
				ADD OPLL_INSTVOL_BASE TO OPLL_REG_OUT;
				MOVE @OPLL_INSTVOL TO OPLL_DATA_OUT;
			END;
			6: BEGIN
				MOVE ^h36 TO OPLL_REG_OUT;
				MOVE GEMINI_COMMAND_DATA TO OPLL_BD_VOL;
				MOVE OPLL_BD_VOL TO OPLL_DATA_OUT;
			END;
			7 - 8: BEGIN
				MOVE ^h37 TO OPLL_REG_OUT;
				IF GEMINI_COMMAND_BYTECODE = 7 THEN
					MOVE GEMINI_COMMAND_DATA TO OPLL_HH_VOL;
				ELSE
					MOVE GEMINI_COMMAND_DATA TO OPLL_SD_VOL;
				FI;
				MOVE OPLL_HH_VOL TO OPLL_DATA_OUT;
				MULTIPLY OPLL_DATA_OUT BY ^h10;
				OR OPLL_DATA_OUT WITH OPLL_SD_VOL;
			END;
			9 - 10: BEGIN
				MOVE ^h38 TO OPLL_REG_OUT;
				IF GEMINI_COMMAND_BYTECODE = 9 THEN
					MOVE GEMINI_COMMAND_DATA TO OPLL_TM_VOL;
				ELSE
					MOVE GEMINI_COMMAND_DATA TO OPLL_CY_VOL;
				FI;
				MOVE OPLL_TM_VOL TO OPLL_DATA_OUT;
				MULTIPLY OPLL_DATA_OUT BY ^h10;
				OR OPLL_DATA_OUT WITH OPLL_CY_VOL;
			END;
			ENDSWITCH;
		CALL SUB_OPLL_WRITE;
	EXIT;	
	
	SUB_SET_DETUNE:
	{--------------------------------}
	{-- SET CHANNEL DETUNE         --}
	{--------------------------------}
		SUBTRACT ^h40 FROM GEMINI_COMMAND_BYTECODE;
		MOVE GEMINI_COMMAND_BYTECODE TO OPLL_DETUNE;
		MULTIPLY OPLL_DETUNE BY 4;
		MOVE GEMINI_COMMAND_DATA TO OPLL_DETUNE;
	EXIT;	
END;
